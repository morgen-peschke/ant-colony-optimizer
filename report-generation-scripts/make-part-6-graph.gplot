#!/usr/bin/env gnuplot

set terminal postscript landscape enhanced color dashed lw 1 "Helvetica" 14
set output 'data/part-6.ps'

set multiplot

set lmargin at screen 0.15
set rmargin at screen 0.90
set bmargin at screen 0.30
set tmargin at screen 0.90

set ytics nomirror 0, 5, 35
unset xtics

set ylabel "Fitness Score"

set xrange [0:281]

a = 0
b = .5

fit_max = 40

datafile = 'data/colony-scores.data'
f(x)     = a * exp (-x * b)
fit  [:fit_max] f(x) datafile using 1:4 via a,b

set title    "Average of top 10% of Fitness Scores in each generation"
data_title = "Collected Data"
fit_title  = sprintf ("fit(x) = %G * e^{(-%f * x)}", a, b)

plot datafile using 1:4 with lines title data_title, \
     0/0 title ' ' with lines lc rgb 'white', \
     f(x) with lines lw 2 lc rgb 'blue' title fit_title

set lmargin at screen 0.40
set bmargin at screen 0.50

unset title
unset xlabel
unset ylabel
set xtics nomirror 0, 5, (fit_max - 5)

set xrange [0:fit_max]

plot [:fit_max] datafile using 1:4 with lines notitle, \
     f(x) with lines lw 2 lc rgb 'blue' notitle

set xlabel "Generation"

set tmargin at screen 0.30
set lmargin at screen 0.15
set bmargin at screen 0.15

set xrange [0:281]
set yrange [0:3]
set ytics nomirror 0, 1, 2
set xtics auto mirror

plot datafile using 1:4 with lines notitle

unset multiplot

set terminal x11
